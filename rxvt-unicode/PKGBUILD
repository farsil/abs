# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org

pkgbase=rxvt-unicode
pkgname=('rxvt-unicode' 'rxvt-unicode-terminfo')
pkgver=9.22
pkgrel=7
arch=('x86_64')
url='http://software.schmorp.de/pkg/rxvt-unicode.html'
group=('abs')
license=('GPL')
makedepends=('libxft' 'perl' 'startup-notification' 'libnsl')
source=(
  "http://dist.schmorp.de/rxvt-unicode/$pkgname-$pkgver.tar.bz2"
  'urxvt.desktop'
  'urxvtc.desktop'
  'font-rendering.patch'
  'clear.patch'
  'secondary-wheel.patch'
  'easy-clipboard.patch'
  'terminfo.patch'
  'intensity.patch'
)
sha256sums=('e94628e9bcfa0adb1115d83649f898d6edb4baced44f5d5b769c2eeb8b95addd'
            '5f9c435d559371216d1c5b49c6ec44bfdb786b12d925d543c286b0764dea0319'
            '91536bb27c6504d6cb0d33775a0c4709a4b439670b900f0c278c25037f19ad66'
            '51c72287d26080c752e073bbb3f47a0745a3510fd3baa6ff153dc91975159762'
            '9ba14e3b9c418d3e5d8932c7096780e30670f1f30387e2dcd5f4f2f5cf61d22b'
            '98bc7d8b5549953d5bf0e68c9a0aab0fc59734099e4caa4eaeae29e5784021dd'
            '12340e7eb9fc709aedc75798e98fb46a162cb9cb472bd216205095ea1b434626'
            '11dbfaf732540f6d7d2dc22b080edfafbf0c20b9f53a907fb36a828412db278a'
            '052dcf47800bca14169748824035d7cac72404f521c81655977e27614ed3568e')

sha512sums=('b39f1b2cbe6dd3fbd2a0ad6a9d391a2b6f49d7c5e67bc65fe44a9c86937f8db379572c67564c6e21ff6e09b447cdfd4e540544e486179e94da0e0db679c04dd9'
            '7184714a908071a4e8e5c065c5f90255e94dfd072df459c8d6f66fca3647781b3d1f6908b9303bcfd0d5b3f2e3822a8d66efaaa8a7c4d44f6e682839031a6e99'
            'aa501eeeb220ba03b3f101b160230612efbca87694fef88c469b2976d29769c24b34576ea82f6c7941fad6039ac776f32e397add9b957b49bf2e84aeb67b66d6'
            'e21d29cd6b68641df2a4f56f61dd3aaf010fa4eb3016e6962ba00e093193381506a0d9eb275f20e968ab3b36583a18822e90c233f006fd01922501194d9714d6'
            '0d089f05ef91bbb976461f504577e50d8488ce8ed2c1faf108ce4b35b8349f1a9b352b285eb8690a6a4db40154287ef8fba6fd86bfc697c783dc99729bea16a4'
            '5ac064749a778e1bce2e1be500fcec0e4f044ba566cb9f4b4abcd3cba49b7d6f2ca96aad7253cca68d6091cb6fe031c515f3958e7c7fdfc9b51eb3525f4345e1'
            '96a4d6b86592038a69d11cfa48a89db98d29404195641630aeee8b95b68fdbd17589e1fc88f0e34ef1c09af855fa1cd0184107a5335cd3909724c685dcd22c0a'
            '4e54e8d35293c14bce8834164ce9be7eaa150ee672304db85ab058099473cbeea2231b1ad613735e274a6dff48eb4165caffa9c1a1b0ca4200492c01ba26fd81'
            '214a647886e69662e1228f9e0b31834f2e2d37fa14226406fa235a0c3dcad1a8ceae5b3ce52c5826706c96e3d705c3c3c89b6bc5de24d9f95403e0aeeb0496d3')

prepare() {
  cd $pkgname-$pkgver

  # Improves font rendering
  patch -p0 -i "$srcdir/font-rendering.patch"

  # Improves handling of screen clear command
  patch -p0 -i "$srcdir/clear.patch"

  # Adds option for scrolling in secondary screen via arrow keys
  patch -p0 -i "$srcdir/secondary-wheel.patch"

  # Adds option for accessing CLIPBOARD via Shift+Ctrl instead of Meta+Ctrl
  patch -p0 -i "$srcdir/easy-clipboard.patch"

  # Improves terminfo setaf/setab
  patch -p0 -i "$srcdir/terminfo.patch"

  # Truecolor sequences should not trigger high intensity colors 
  patch -p0 -i "$srcdir/intensity.patch"
}

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --enable-256-color \
    --disable-combining \
    --disable-fading \
    --disable-font-styles \
    --disable-iso14755 \
    --disable-keepscrolling \
    --disable-lastlog \
    --enable-mousewheel \
    --disable-next-scroll \
    --disable-perl \
    --disable-pointer-blank \
    --disable-rxvt-scroll \
    --enable-selectionscrolling \
    --enable-slipwheeling \
    --disable-smart-resize \
    --enable-startup-notification \
    --disable-transparency \
    --disable-unicode3 \
    --disable-utmp \
    --disable-wtmp \
    --enable-xft \
    --enable-xim \
    --disable-xterm-scroll \
    --disable-pixbuf \
    --disable-frills

  make
}

package_rxvt-unicode() {
  pkgdesc='Unicode enabled rxvt-clone terminal emulator (urxvt)'
  depends=('rxvt-unicode-terminfo' 'libxft' 'perl' 'startup-notification' 'libnsl')

  cd "$pkgname-$pkgver"

  # install freedesktop menu
  install -Dm644 -t "$pkgdir/usr/share/applications" "$srcdir/urxvt.desktop"
  install -Dm644 -t "$pkgdir/usr/share/applications" "$srcdir/urxvtc.desktop"

  # tells tic to compile in $srcdir/terminfo
  export TERMINFO="$srcdir/terminfo"
  mkdir -p "$TERMINFO"

  # install package
  make DESTDIR="$pkgdir" install
}

package_rxvt-unicode-terminfo() {
  pkgdesc='Terminfo files for urxvt'
  conflict=('rxvt-unicode<=9.18-6')

  # this should be made by rxvt-unicode's make
  cd "$srcdir/terminfo/r"

  # install terminfo files
  install -Dm644 -t "$pkgdir/usr/share/terminfo/r" rxvt-unicode
  install -Dm644 -t "$pkgdir/usr/share/terminfo/r" rxvt-unicode-256color
}

# vim:set ts=2 sw=2 et:
